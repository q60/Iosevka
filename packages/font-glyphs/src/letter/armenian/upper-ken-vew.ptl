$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [DependentSelector] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Armenian-Upper-Ken-Vew : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : uBowl SerifFrame
	glyph-block-import Letter-Armenian-Shared-Shapes : ArmHBar

	do "Ken"
		create-glyph 'armn/Ken' 0x53F : glyph-proc
			local df : include : DivFrame 1
			include : df.markSet.capital
			include : uBowl.shape
				top    -- CAP
				bottom -- (XH / 2 - df.mvs / 2)
				left   -- df.leftSB
				right  -- (df.rightSB - [HSwToV df.mvs])
				ada    -- ArchDepthA
				adb    -- ArchDepthB
				stroke -- df.mvs
			include : VBar.r df.rightSB 0 XH df.mvs
			if SLAB : begin
				local sf : SerifFrame.fromDf df CAP 0
				local sf2 : SerifFrame.fromDf df XH 0
				include : composite-proc sf.lt.full sf2.rt.full sf.rb.full

	do "Vew"
		create-glyph 'armn/Vew' 0x54E : glyph-proc
			local df : include : DivFrame 1
			include : df.markSet.capital
			include : uBowl.shape
				top    -- XH
				bottom -- (XH / 2 - df.mvs / 2)
				left   -- df.leftSB
				right  -- (df.rightSB - [HSwToV df.mvs])
				ada    -- ArchDepthA
				adb    -- ArchDepthB
				stroke -- df.mvs
			include : VBar.r df.rightSB 0 CAP df.mvs
			include : [ArmHBar.right df].base
			if SLAB : begin
				local sf : SerifFrame.fromDf df XH 0
				local sf2 : SerifFrame.fromDf df CAP 0
				include : composite-proc sf.lt.full sf2.rt.full sf.rb.full
